extern (C) putchar (const c : char);
extern (C) printf (const c : p!char, ...);

def print (c : char) -> void
    putchar (c);

def print (const s : string) -> void {
    let i = 0U;
    while i < s.len {
        print (s [i]);
        i = i + 1U;
    }
}

def print (i : long) ->  void {
    printf ("%ld".ptr, i);
}

def print (i : int) ->  void {
    printf ("%d".ptr, i);
}

def print (i : short) ->  void {
    printf ("%hd".ptr, i);
}

def print (i : byte) ->  void {
    printf ("%hhx".ptr, i);
}

def print (i : ulong) ->  void {
    printf ("%lu".ptr, i);
}

def print (i : p!void) ->  void {
    printf ("%lu".ptr, i);
}

def print (T) (i : p!T) ->  void {
    printf ("%lu".ptr, i);
}

def print (i : uint) ->  void {
    printf ("%u".ptr, i);
}

def print (i : ushort) ->  void {
    printf ("%hu".ptr, i);
}

def print (i : ubyte) -> void {
    printf ("%hhx".ptr, i);
}

def print (a : float, prec : long) {
    printf ("%.*lf".ptr, prec, a);
}

def print (a : double, prec : long) {
    printf ("%.*lf".ptr, prec, a);
}

def print (f : float) {
    printf ("%lf".ptr, f);
}

def print (f : double) {
    printf ("%lf".ptr, f);
}

def print (b : bool) {
    if (b) print ("true");
    else print ("false");
}

def print (T of [U], const U) (a : const T) {
    let i = 0U;
    print ('[');
    while i < a.len {
        print (a [i]);
        if i < a.len - 1U
            print (", ");
        i = i + 1U;
    }
    print (']');
}

def println (T) (const i : T) {
    print (i);
    print ('\n');
}

def print (T) (const tu : t!(const T)) {
    print (tu.0);
}

def print if (is (T : tuple)) (T) (const tu : T) {
    let (a, b...) = tu;
    print (a);
    print (b);    
}

private def printTuple (T) (const tu : t!(T)) {
    print (tu.0);
}

private def printTuple if (is (T : tuple)) (T) (const tu : T) {
    let (a, b...) = tu;
    print (a);
    print (", ");
    printTuple (b);    
}

def print if (is (T : struct)) (T) (const a : T) {
    print (a::typeid);
    print ('(');
    printTuple (a.tupleof);
    print (')');
}

def print (T, N : ulong) (const a : [const T ; N]) {
    print ('[');
    for it in 0U .. a.len {
        print (a [it]);
        if it < a.len - 1U
            print (", ");
    }
    print (']');
}
