mod game.movement;

import std.math;

enum
| Right : 0
| Left : 1
| Down : 2
 -> MoveDirection;

struct
| vX : f32
| vY : f32
| speedInit : i32
| angleInit : f32
| timer : i32   
| startPosX : i32
| startPosY : i32
| lastPosY : i32
| horizontalForce : f32
| verticalForce : f32
 -> ParabolicMovement;

struct
| posX : i32
| posY : i32
| speedX : i32
| speedY : i32
 -> LinearMovement;

imut gG = 9.81f;

def abs (T) (value : T) -> T
{
    if (value < 0)
        return (value * -1);
    return value;
}

/* ### Linear Movement Management  */

def createLinearMovement (posX : i32, posY : i32) -> mut LinearMovement
{
    let mvt = LinearMovement::init;
    mvt.start (posX, posY);
    
    return mvt;
}

def start (ref movement : LinearMovement, posX : i32, posY : i32)
{
    movement.posX = posX;
    movement.posY = posY;
}

def move (ref movement : LinearMovement, const speed : i32, const direction : MoveDirection)
{
    match (direction)
    {
        MoveDirection::Right => movement.speedX = speed;
        MoveDirection::Left  => movement.speedX = -(abs (speed));
        MoveDirection::Down  => movement.speedY = speed;
    }
}

def stop (ref movement : LinearMovement, const direction : MoveDirection)
{
    match (direction)
    {
        MoveDirection::Right | MoveDirection::Left => movement.speedX = 0;
        MoveDirection::Down => movement.speedY = 0;
    }
}

def nextPosition (ref movement : LinearMovement) -> mut t!(i32, i32)
{
    movement.posX += movement.speedX;
    movement.posY += movement.speedY;

    if (movement.speedX > 0)
        movement.speedX--;
    else if (movement.speedX < 0)
        movement.speedX++;

    return (movement.posX, movement.posY);
}

/* ### Parabolic Movement Management  */

def start (ref movement : ParabolicMovement, posX : i32, posY : i32, speedInit : i32, angleInit : f32)
{
    movement.speedInit = speedInit;
    movement.angleInit = angleInit;
    movement.verticalForce = 4.0f;

    extern (C) cosf (value : f32) -> f32;
    extern (C) sinf (value : f32) -> f32;
    
    movement.vX = cosf (angleInit) * (abs (movement.speedInit));
    movement.vY = sin (angleInit) * (abs (movement.speedInit));

    movement.startPosX = posX;
    movement.startPosY = posY;

    movement.timer = 0;
}

def nextPosition (ref movement : ParabolicMovement) -> t!(i32, i32)
{
    // Relative position
    let relX = cast!i32 ((movement.vX * movement.timer) - ((movement.horizontalForce * pow (movement.timer, 2u)) / 2000));
    let relY = cast!i32 ((movement.vY * movement.timer) - (((gG + movement.horizontalForce) * pow (movement.timer, 2u)) / 2000));

    // Aboslute position
    let absX = i32::init;
    let absY = i32::init;

    if (movement.speedInit < 0)
        absX = movement.startPosX - relX;
    else
        absX = movement.startPosX + relX;

    absY = movement.startPosY - relY;

    // TODO : better idea ?
    movement.timer += 2;

    return (absX, absY);
}
